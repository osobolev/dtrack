plugins {
    id("com.github.ben-manes.versions") version "0.45.0"
}

def getMajor(version, majorDepth) {
    int p = -1
    for (int i = 0; i < majorDepth; i++) {
        p = version.indexOf('.', p + 1)
        if (p < 0) return version
    }
    return p < 0 ? "" : version.substring(0, p)
}

def getMajorDepth(mod) {
    if (mod.group == "org.slf4j") return 2
    if (mod.group == "org.json") return 0
    if (mod.group == "com.googlecode.owasp-java-html-sanitizer") return 0
    return 1
}

tasks.withType(com.github.benmanes.gradle.versions.updates.DependencyUpdatesTask).configureEach {
    resolutionStrategy {
        componentSelection {
            all {
                def majorDepth = getMajorDepth(candidate)
                if (getMajor(candidate.version, majorDepth) != getMajor(currentVersion, majorDepth)) {
                    reject("Major update")
                }
            }
        }
    }
}

subprojects {
    group = "dtrack"
    version = "1.0-SNAPSHOT"

    apply(plugin: "java-library")
    
    java {
        sourceCompatibility = "1.8"
    }
    compileJava.options.encoding = "UTF-8"
    compileTestJava.options.encoding = "UTF-8"

    sourceSets.main.java.srcDirs = ["src"]
    sourceSets.main.resources.srcDirs = ["resources"]
    sourceSets.test.java.srcDirs = ["test"]

    repositories {
        mavenCentral()
    }
}

project(":") {
    task clean(type: Delete) {
        delete("distr")
    }
}
